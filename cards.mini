def (
	DrawPile, Hand, DiscardPile, Modifiers, Counters, Goals,
	card, current, increase, decrease, goal, progress, draw, discard, remove, choose, log
) in import("game")

enum: size("Tiny", "Small", "Medium", "Large", "Huge")

export: class: work(card)
meth :playable(Card: work) Card::Cost <= current("energy")

export: class: task(work)
meth :name(Card: task) 'Task-{Card::Skill}'
meth :description(Card: task) 'A task requiring {Card::Cost} energy and {Card::Skill} skill.'
meth :play(Card: task) do
	decrease("energy", Card::Cost)
	remove(Card)
end
meth :remove(Card: task) do
	progress(Card::Goal, Card::Index)
end

export: class: developer(card)
meth :name(Card: developer) 'Developer-{Card::Skill}'
meth :description(Card: developer) 'A developer with {Card::Skill} skill.'
meth :playable(Card: developer) 1
meth :play(Card: developer) do
	let Skill := Card::Skill + (current("energy") min 2)
	let Task := choose(Hand ->? fun(Card) Card in task and Card::Skill <= Skill)
	if Task then
		let Cost := Task::Skill - Card::Skill
		if Cost > 0 then
			decrease("energy", Cost)
			Card::Skill := old + 1
		end 
		progress(Task::Goal, Task::Index)
		remove(Task)
	end
	discard(Card)
end

export: class: solution(card)
meth :name(Card: solution) 'Solution-{Card::Value}'
meth :description(Card: solution) 'A solution generating {Card::Value} value.'
meth :playable(Card: solution) 1
meth :play(Card: solution) do
	increase("value", Card::Value)
	discard(Card)
end

export: class: project(work)
meth :name(Card: project) '{size(Card::Size)} Project'
meth :description(Card: project) '{size(Card::Size)} project.'
meth :play(Card: project) do
	decrease("energy", Card::Cost)
	let Project := sum(1 .. 8;) "ABCDEFGHIJKLMNOPQRSTUVWXYZ":random
	let Tasks := list(1 .. Card::Size; I) do
		discard(task({"Cost" is 1, "Skill" is 3, "Goal" is Project, "Index" is I}))
	end
	goal(Project, set(1 .. Card::Size); Goal) do
		discard(solution({"Value" is Card::Size}))
	end
	remove(Card)
end

export: class: meeting(work)
meth :name(Card: meeting) 'Meeting'
meth :description(Card: meeting) 'Recurring meeting requiring {Card::Cost} energy.'
meth :play(Card: meeting) do
	decrease("energy", Card::Cost)
	discard(Card)
end

export: class: training(work)
meth :name(Card: training) 'Training'
meth :description(Card: training) 'Mandatory training requiring {Card::Cost} energy.'
meth :play(Card: training) do
	decrease("energy", Card::Cost)
	remove(Card)
end
meth :blocks(Card: training, Other: card) Other in task

export: class: audience(card)
meth :demo(Card: audience) nil

export: class: demo(card)
meth :name(Card: demo) 'Demo-{Card::Value}'
meth :description(Card: demo) 'A demo generating {Card::Value} influence / projects.'
meth :playable(Card: demo) some(Hand, _ in audience)
meth :play(Card: demo) do
	let Audience := choose(Hand ->? fun(Card) Card in audience)
	Audience:demo(Card)
end

export: class: bigshot(audience)
meth :name(Card: bigshot) 'Bigshot'
meth :description(Card: bigshot) 'Visiting bigshot requires meeting costing {Card::Cost} energy or demo.'
meth :playable(Card: bigshot) Card::Cost <= current("energy")
meth :play(Card: bigshot) do
	decrease("energy", Card::Cost)
	increase("influence", 1)
	discard(Card)
end
meth :blocks(Card: bigshot, Other: card) Other in work
meth :demo(Card: bigshot, Demo: demo) do
	increase("influence", Demo::Value)
	remove(Demo)
	discard(Card)
end

export: class: customer(work, audience)
meth :name(Card: customer) 'Customer-{Card::Value}'
meth :description(Card: customer) 'Customer that can generate ideas of value {Card::Value}.'
meth :play(Card: customer) do
	decrease("energy", Card::Cost)
	discard(idea({"Cost" is 1, "Size" is Card::Value}))
	discard(Card)
end
meth :demo(Card: customer, Demo: demo) do
	remove(Demo)
	discard(Card)
	discard(project({"Cost" is 1, "Size" is Demo::Value}))
end

export: class: idea(work)
meth :name(Card: idea) 'Idea-{Card::Size}'
meth :description(Card: idea) 'An idea for a demo that will generate {Card::Size} influence / projects.'
meth :play(Card: idea) do
	decrease("energy", Card::Cost)
	let Idea := sum(1 .. 8;) "ABCDEFGHIJKLMNOPQRSTUVWXYZ":random
	let Tasks := list(1 .. Card::Size; I) do
		discard(task({"Cost" is 1, "Skill" is 3, "Goal" is Idea, "Index" is I}))
	end
	goal(Idea, set(1 .. Card::Size); Goal) do
		discard(demo({"Value" is Card::Size}))
	end
	remove(Card)
end

export: class: recruiter(card)
meth :name(Card: recruiter) 'Recruiter'
meth :description(Card: recruiter) 'A recruiter costing {Card::Cost} influence to add a developer to the deck.'
meth :playable(Card: recruiter) Card::Cost <= current("influence")
meth :play(Card: recruiter) do
	decrease("influence", Card::Cost)
	discard(developer({"Skill" is 1}))
	discard(Card)
end

export: class: workshop(card)
meth :name(Card: workshop) 'Workshop-{Card::Value}'
meth :description(Card: workshop) 'A workshop costing {Card::Cost} energy.'
meth :playable(Card: workshop) Card::Cost <= current("energy")
meth :play(Card: workshop) do
	decrease("energy", Card::Cost)
	for Card in DrawPile & Hand & DiscardPile ->? (_ in developer) do
		Card::Skill := old + 1 min 5
	end
	increase("influence", Card::Value)
	if some(Hand, _ in bigshot) then
		increase("influence", 1)
	end
	remove(Card)
end

export: class: presentation(work)
meth :name(Card: presentation) 'Presentation'
meth :description(Card: presentation) 'A presentation costing {Card::Cost} energy. Generates {Card::Value} influence and Increases the skill of every developer in your hand.'
meth :playable(Card: presentation) Card::Cost <= current("energy")
meth :play(Card: presentation) do
	decrease("energy", Card::Cost)
	increase("influence", Card::Value)
	for Card in Hand ->? (_ in developer) do
		Card::Skill := old + 1 min 5
	end
	discard(Card)
end

export: class: plan(work)
meth :name(Card: plan) 'Plan {Card::Goal:name}'
meth :description(Card: plan) 'Plan a {Card::Goal:name}.'
meth :play(Card: plan) do
	decrease("energy", Card::Cost)
	let Goal := sum(1 .. 8;) "ABCDEFGHIJKLMNOPQRSTUVWXYZ":random
	for I in 1 .. Card::Size do
		discard(task(Card::Task + {"Goal" is Goal, "Index" is I}))
	end
	goal(Goal, set(1 .. Card::Size); Goal) do
		discard(Card::Goal(Card::Details))
	end
	discard(Card)
end

export: class: coffee(card)
meth :name(Card: coffee) 'Coffee'
meth :description(Card: coffee) 'Increase energy and draw a card.'
meth :playable(Card: coffee) 1
meth :play(Card: coffee) do
	increase("energy", 1)
	remove(Card)
	draw()
end

export: class: order(card)
meth :name(Card: order) 'Order {Card::Size} {Card::Order:name}'
meth :description(Card: order) 'Orders {Card::Size} {Card::Order:name}.'
meth :playable(Card: order) Card::Cost <= current("energy")
meth :play(Card: order) do
	decrease("energy", Card::Cost)
	for I in 1 .. Card::Size do
		discard(Card::Order(Card::Details or {}))
	end
	discard(Card)
end

export: class: annoyance(work)

export: class: overhead(annoyance, work)
meth :name(Card: overhead) '{if Card::Mandatory = true then "Mandatory " else "" end}Overhead'
meth :description(Card: overhead) 'Overhead requiring {Card::Cost} energy. Draw a card when played.'
meth :play(Card: overhead) do
	decrease("energy", Card::Cost)
	discard(Card)
	draw()
end
meth :blocks(Card: overhead, Other: card) Card::Mandatory = true and Other in work and not Other in overhead

export: class: practioner(card)
meth :name(Card: practioner) 'Agile practitioner'
meth :description(Card: practioner) 'Agile practioner generates overhead when discarded.'
meth :discard(Card: practioner) do
	discard(overhead({"Cost" is 1, "Influence" is 1, "Mandatory" is random(boolean)}))
end

export: class: retrospective(card)
meth :name(Card: retrospective) 'Retrospective'
meth :description(Card: retrospective) 'Team retrospective. Uses influence to remove overhead and practioners.'
meth :playable(Card: retrospective) 1
meth :play(Card: retrospective) do
	break()
	let Deck := DrawPile & Hand & DiscardPile
	loop		
		let Annoyance := while choose(Deck ->? fun(Card) Card in annoyance and Card::Cost <= current("influence"), ::Influence)
		decrease("influence", Annoyance::Influence)
		remove(Annoyance)
	end
	discard(Card)
end

export: class: event(card)
meth :name(Card: event) 'Event'
meth :description(Card: event) 'Event.'
meth :draw(Card: event) do
	log('\e[33mEvent\e[0m \e[4m{Card:name}\e[0m played from hand.')
	discard(Card)
	for (Prob, Type, Details) in Card::Events do
		if random(real) < Prob then
			draw(Type(Details))
		end
	end
end
