export: let DrawPile := []
export: let Hand := []
export: let DiscardPile := []
export: let Modifiers := []
export: let Counters := {}
export: let Goals := {}

export: var choose
export: var display
export: var log

class: blocker(:type, :reason)

export: class: card(:details)

meth ::(Card: card, Detail: string) Card:details[Detail]

meth :name(Card: card) Card::Name or type(Card):name:title
meth :description(Card: card) Card::Description or 'A {type(Card):name}.'
meth :draw(Card: card) nil
meth :blocks(Card: card, Other: card) nil
meth :blocked(Card: card) 'is unplayable'
meth :play(Card: card) nil
meth :discard(Card: card) nil
meth :remove(Card: card) nil

export: fun blocked(Card) do
	for Modifier in Modifiers do
		if Modifier in blocker and Card in Modifier:type then
			ret Modifier:reason
		end
	end
	for Other in Hand do
		if let Reason := Other:blocks(Card) then
			ret 'by \e[4m{Other:name}\e[24m'
		end
	end
	ret Card:blocked
end

export: fun played(Card) do
	
end

export: fun current(Name) Counters[Name]

export: fun increase(Name, Value) do
	:> TODO: Check for modifiers
	Counters[Name] := old + Value
end

export: fun decrease(Name, Value) do
	:> TODO: Check for modifiers
	Counters[Name] := old - Value
end

export: fun goal(Name, Tasks, Action) do
	Goals[Name] := (Tasks, Action)
end

export: fun progress(Name, Index) do
	let (Tasks, Goal) := Goals[Name] or error("NameError", 'Unknown goal: {Name}')
	Tasks:delete(Index)
	if Tasks:size = 0 then
		Goals:delete(Name)
		discard(Goal)
	end
end

export: fun draw(Card := DrawPile[1]) do
	if Card then
		log('\e[32mAdded\e[0m \e[4m{Card:name}\e[0m to hand.')
		DrawPile:filter(_ != Card)
		DiscardPile:filter(_ != Card)
		Hand:put(Card)
		Card:draw
	end
	ret Card
end

export: fun discard(Card) do
	:> TODO: Check for modifiers
	DrawPile:filter(_ != Card)
	if Hand:filter(_ != Card):length = 0 then
		log('\e[32mAdded\e[0m \e[4m{Card:name}\e[0m to discard pile.')
	end
	DiscardPile:put(Card)
	Card:discard
end

export: fun remove(Card) do
	:> TODO: Check for modifiers
	DrawPile:filter(_ != Card)
	Hand:filter(_ != Card)
	DiscardPile:filter(_ != Card)
	log('\e[31mRemoving\e[0m \e[4m{Card:name}\e[0m from hand.')
	Card:remove
end

export: fun start(DayFn, TurnFn) do
	for Day in 1:up do
		let DrawCount := DayFn(Day)
		for I in 1 .. DrawCount do
			while draw()
		end
		loop
			TurnFn(Day)
			if let Card := choose("Select card to play", Hand, fun(Card) Card::Cost{} * "â¬®") then
				if let Reason := blocked(Card) then
					log('\e[33mBlocked\e[0m \e[4m{Card:name}\e[0m {Reason}.')
				else
					Card:play
				end
			else
				loop
					discard(while Hand[1])
				end
				if DrawPile:length = 0 then
					DrawPile:grow(DiscardPile)
					DrawPile:shuffle
					DiscardPile:empty
				end
				exit
			end
		on Error do
			display('\e[31mError\e[0m {Error}\n')
			log('\e[31mError\e[0m {Error}')
		end
	end
end
